apply plugin: 'com.android.application'
apply plugin: 'android-apt'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs {
        def path = properties.getProperty("keystore.path")
        def password = properties.getProperty("keystore.password")
        def alias = properties.getProperty("keystore.alias")
        debugSign {
            keyAlias alias
            keyPassword password
            storeFile file(path)
            storePassword password
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.hahaxueche"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 35
        versionName "3.3.5"
        multiDexEnabled true
    }
    // 友盟多渠道打包
    productFlavors {
        wandoujia {}
        _360 {}
        xiaomi {}
        yingyongbao {}
        haha {}
        _91 {}
        anzhuoshichang {}
        baiduzhushou {}
        huawei {}
        vivo {}
        meizu {}
        sougou {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", properties.getProperty("serverUrl")
            buildConfigField "String", "MOBILE_URL", properties.getProperty("mobieUrl")
            buildConfigField "String", "MI_PUSH_APP_ID", properties.getProperty("miPushAppId")
            buildConfigField "String", "MI_PUSH_APP_KEY", properties.getProperty("miPushAppKey")

        }
        debug {
            signingConfig signingConfigs.debugSign
            buildConfigField "String", "SERVER_URL", properties.getProperty("serverUrl")
            buildConfigField "String", "MOBILE_URL", properties.getProperty("mobieUrl")
            buildConfigField "String", "MI_PUSH_APP_ID", properties.getProperty("miPushAppId")
            buildConfigField "String", "MI_PUSH_APP_KEY", properties.getProperty("miPushAppKey")
        }
    }
    dexOptions {
        javaMaxHeapSize "4g" //specify the heap size for the dex process
    }
}

repositories {
    flatDir {
        dirs'libs'
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    //rxjava+retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    //banner
    compile 'com.bigkoo:convenientbanner:2.0.5'
    compile 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
    //butterknife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    //fresco
    compile 'com.facebook.fresco:fresco:0.12.0'
    compile 'com.facebook.fresco:animated-gif:0.12.0'
    //手机号校验
    compile 'com.googlecode.libphonenumber:libphonenumber:7.6.1'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    //七鱼客服
    compile 'com.qiyukf.unicorn:unicorn:2.7.0'
    //okhttp日志
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    //long img
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    //ping++
    compile 'com.pingxx:pingpp-core:2.1.+'
    compile 'com.pingxx:pingpp-alipay:2.1.+'
    //自适应textview
    compile 'me.grantland:autofittextview:0.2.+'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //内存泄漏检测
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //pdf viewer
    compile 'com.github.barteksc:android-pdf-viewer:2.3.0'
    compile 'com.google.zxing:android-core:3.3.0'
    compile 'com.google.zxing:core:3.3.0'
    //AndFix
    compile(name:'alisdk-hotfix-1.4.0', ext:'aar')
}
